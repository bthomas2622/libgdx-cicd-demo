plugins { id 'org.beryx.runtime' version '1.8.4' }

apply plugin: 'application'

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
mainClassName = 'io.itch.freebrunch.lwjgl3.Lwjgl3Launcher'
def osName = System.getProperty('os.name').toLowerCase(Locale.ROOT)
eclipse.project.name = appName + '-lwjgl3'
sourceCompatibility = 16.0

dependencies {
	implementation project(':core')
	implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
	implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
	implementation "com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdxControllersVersion"
	implementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
}

import org.gradle.internal.os.OperatingSystem

run {
	workingDir = rootProject.file('assets').path
	setIgnoreExitValue(true)

	if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
		// Required to run LWJGL3 Java apps on MacOS
		jvmArgs += "-XstartOnFirstThread"
	}
}

jar {
// sets the name of the .jar file this produces to the name of the game or app.
	archiveBaseName.set(appName)
// the duplicatesStrategy will matter starting in Gradle 7.0; this setting works.
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
	dependsOn configurations.runtimeClasspath
	from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
// these "exclude" lines remove some unnecessary duplicate files in the output JAR.
	exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
	dependencies {
		exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
	}
// setting the manifest makes the JAR runnable.
	manifest {
		attributes 'Main-Class': project.mainClassName
	}
	destinationDirectory = file("$buildDir/lib")
// this last step may help on some OSes that need extra instruction to make runnable JARs.
	doLast {
		file(archiveFile).setExecutable(true, false)
	}
}

jpackageImage.dependsOn jar

runtime {
	options = ['--strip-debug',
			   '--compress', '2',
			   '--no-header-files',
			   '--no-man-pages',
			   '--strip-native-commands',
			   '--vm', 'server']
	modules = ['java.base' ,
			   'java.desktop',
			   'jdk.unsupported']
	distDir = file(buildDir)

	jpackage {
		//jpackageHome = '/usr/lib/jvm/open-jdk'
		mainJar = jar.archiveFileName.get()
		if (osName.contains('windows')) {
			imageOptions = ["--icon", file("../icons/icon.ico")]
		} else if (osName.contains('linux')) {
			imageOptions = ["--icon", file("../icons/icon.png")]
		} else if (osName.contains('mac')) {
			imageOptions = ["--icon", file("../icons/icon.icns")]
		}
	}
}

